#!perl
use 5;
use warnings;
use YAML::Tiny;
use LWP::UserAgent; # For fetching stats from lists.apache.org
use JSON;
use MentorStats;
use Data::Dumper;

my $proj = shift || help();

my $ua = LWP::UserAgent->new;

my $project = MentorStats::getproject( $proj );
my @repos = @{$project->{repos}};

print $project->{name} . "\n";

# Fetch all PRs, across all open repos
my @prs = MentorStats::getprs( @repos );

# Check mailing list activity
my @devs =  sort ( @{$project->{devs}} );
my @lists = @{$project->{lists}};

if (@lists) { # Only do this for ASF projects with mailing lists
    foreach my $dev (@devs) {

        my $d = MentorStats::getdev($dev);
        my @posts;

        unless ( $d->{name} ) {
            warn "Data missing for $dev\n";
            next;
        }

        # Count posts to the various mailing lists
# TODO: Move this into MentorStats.pm
        foreach my $list (@lists) {
            my $mlurl =
                "https://lists.apache.org/api/stats.lua?list="
              . $list
              . "&d=lte=3M"
              . "&domain="
              . $project->{domain}
              . ".apache.org&header_from="
              . $d->{email};

            my $req = HTTP::Request->new( GET => $mlurl );
            my $res = $ua->request($req);

            # check the outcome
            if ( $res->is_success ) {
                my $content = $res->decoded_content;
                my $stats   = JSON->new->decode($content);

                # Have they ever posted to this list?
                if ( %{ $stats->{active_months} } ) {

                    push @posts, $list . '@' . $project->{domain} . ".apache.org:";
                    foreach my $k ( sort ( keys %{ $stats->{active_months} } ) )
                    {
                        push @posts, $k . ": " . $stats->{active_months}->{$k};
                    }
                }
            } else {
                warn "Error: " . $res->status_line . "\n";
            }

        }

        # Next, see what their GitHub stats look like
        my @myprs;
        foreach my $pull (@prs) {
            if ( $pull->{head}->{user}->{login} eq $d->{githubid} ) {
                push @myprs, $pull;
            }
        }

        # Skip people with no activity
        if ( @posts || @myprs ) {
            print "\n" . $d->{name} . "\n============\n\n";
        }

        # Have they ever posted to the mailing list?
        if (@posts) {
            foreach my $p ( @posts ) {
                print $p . "\n";
            }
            print "\n";
        }

        # Only display this section if there are any.
        if (@myprs) {
            print "Open PRs:\n";
            foreach my $pull (@myprs) {
                print $pull->{html_url} . " (" . $pull->{created_at} . ")\n";
            }
        }
    }
}

sub help {
    print <<'ENDEND';
For ASF projects, for a specified list of developers,
displays their activity on mailing lists, and open PRs.

ENDEND

    print "\nData is stored in $DATA\n";

    print "Usage: $0 project_name\n\n";
    print "Available projects:\n";

    my $pdir = $DATA . '/projects';
    opendir ( DIR, $pdir ) or die "Could not open $pdir\n";

    while ( my $projfile = readdir(DIR) ) {
        next unless $projfile =~ m/\.yml$/;
        $projfile =~ s/\.yml//;
        print "* " .$projfile."\n";
    }
    print "\n";
    exit();
}


